```
/// 获得指定院区的空床位
/// 参数1：医院id
/// 参数2：病区描述（为空则统计所有的，不为空则统计包含描述的病区）
/// 参数3：是否显示明细（1，显示明细，用于查看统计是否正常）
/// ##class(Nur.Data.UnitMethod).getValidBedNum(2,"",1)
ClassMethod getValidBedNum(HospitalId, WardDesc = "", IfShowInfo = "")
{
	s num = "0"
	s dateL = $p($h,",",1)
	s timeL = $p($h,",",2)
	s dateTimeL = dateL*100000+timeL
	s locId = "" f{
		s locId = $o(^CTLOC("0","Hosp",HospitalId,locId))
		q:locId=""
		s locData = ^CTLOC(locId)
		s ifWard = $p(locData,"^",5)
		continue:ifWard="N"
		s locDesc = $p(locData,"^",2)
		s wardId = "" f{
			s wardId = $o(^PAWARD(0,"WARD_LocationDR",locId,wardId))
			q:wardId=""
			//病区是否可用
			s wardActive = $p(^PAWARD(wardId),"^",6)
			continue:wardActive="N"
			//当前时间病区是否有效
			s toDateL = $p(^PAWARD(wardId),"^",9)
			s toTimeL = $p(^PAWARD(wardId),"^",10)
			s toDateTimeL = toDateL*100000+toTimeL
			continue:(toDateTimeL'=0)&&(toDateTimeL<dateTimeL)
			//是否筛选病区
			s wardDesc = $p(^PAWARD(wardId),"^",2)
			continue:(WardDesc'="")&&(wardDesc'[WardDesc)
			s bedSid = "0" f{
				s bedSid = $o(^PAWARD(wardId,"BED",bedSid))
				q:bedSid=""
				s bedData=^PAWARD(wardId,"BED",bedSid)
				s bedRcFlag = $p(bedData,"^",4)
				continue:bedRcFlag="N" //不激活不显示
				//有人不显示,根据表PacBedAdm
				s pacBedSid = $g(^PAWARDA(wardId,"BED",bedSid,"ADM",0))
				continue:(pacBedSid'="")&&($d(^PAWARDA(wardId,"BED",bedSid,"ADM",pacBedSid)))
				s bedCode = $p(^PAWARD(wardId,"BED",bedSid),"^",1)
				s num = num + 1
				i IfShowInfo=1 w wardDesc_" "_bedCode,!
			}
		}
	}
	q num
}

```

RB_Resource

```
ClassMethod GetPracticeDoctors(locID) As %ListOfDataTypes
{
	s curDate=+$h
	k mainDocsArr
	s mainDocs=##class(%ListOfDataTypes).%New()

	s resourceID=0
	f  s resourceID=$o(^RB("RES",0,"CTLOC",locID,resourceID)) q:resourceID=""  d
	.s careProvID=$p($G(^RB("RES",resourceID)),"^",2)
	.s admRight=$p($G(^RB("RES",resourceID)),"^",20)
	.q:(admRight'="Y")
	.s resourceDateFrom=$p($G(^RB("RES",resourceID)),"^",22)
	.s resourceDateTo=$p($G(^RB("RES",resourceID)),"^",23)
	.q:((resourceDateFrom'="")&&(curDate<resourceDateFrom))||((resourceDateTo'="")&&(curDate>resourceDateTo))
	.s docName=$p($G(^RB("RES",resourceID)),"^",17)
	.s docName=##class(User.CTCareProv).GetTranByDesc("CTPCPDesc",docName)
	.q:careProvID=""
	.q:$d(mainDocsArr(+careProvID))
	.s docCode=$P($G(^CTPCP(careProvID,1)),"^",1)
	.s careProvTypeID=$P($G(^CTPCP(careProvID,1)),"^",4)
	.q:careProvTypeID=""
	.s activeFlag=$P($G(^CTPCP(careProvID,1)),"^",9)
	.q:activeFlag="N"
	.s careProvType=$P($G(^CT("CPT",careProvTypeID)),"^",4)
	.q:careProvType'="DOCTOR"
	.s SSUSRRowId=$o(^SSU("SSUSR",0,"CTPCP",careProvID,""))
	.q:SSUSRRowId=""
	.s SSUSRDateFrom=$p($g(^SSU("SSUSR",SSUSRRowId)),"^",96)		//开始日期
	.s SSUSRDateTo=$p($g(^SSU("SSUSR",SSUSRRowId)),"^",97)			//结束日期
	.q:((SSUSRDateFrom'="")&&(curDate<SSUSRDateFrom))||((SSUSRDateTo'="")&&(curDate>SSUSRDateTo))
	.s SSUSRDefaultDeptDR=$p($g(^SSU("SSUSR",SSUSRRowId)),"^",4)    //登录科室DR
	.s OTHLLStartDate="",OTHLLEndDate="",OTHLLCTLOCDR="",available=-1
	.i SSUSRDefaultDeptDR'=locID d
	..s ChildSub =0
	..f  s ChildSub=$o(^SSU("SSUSR",SSUSRRowId,"OTHLL",ChildSub)) q:(ChildSub="")||(available=1)  d
	...s OTHLLCTLOCDR=$p($g(^SSU("SSUSR",SSUSRRowId,"OTHLL",ChildSub)),"^",1)
	...i OTHLLCTLOCDR=locID d
	....s available=1
	....s OTHLLStartDate=$p($g(^SSU("SSUSR",SSUSRRowId,"OTHLL",ChildSub)),"^",4)
	....s OTHLLEndDate=$p($g(^SSU("SSUSR",SSUSRRowId,"OTHLL",ChildSub)),"^",5)
	....i ((OTHLLStartDate'="")&&(curDate<OTHLLStartDate))||((OTHLLEndDate'="")&&(curDate>OTHLLEndDate)) s available=0
	.q:available=0
	.s mainDoc=##class(%ArrayOfDataTypes).%New()
	.d mainDoc.SetAt(docName,"name")
	.d mainDoc.SetAt(+careProvID,"ID")
	.d mainDoc.SetAt(docCode,"jobNo")
	.d mainDoc.SetAt(locID,"locID")
	.s mainDocsArr(+careProvID)=1
	.d mainDocs.Insert(mainDoc)
	k mainDocsArr
	q mainDocs
}
```
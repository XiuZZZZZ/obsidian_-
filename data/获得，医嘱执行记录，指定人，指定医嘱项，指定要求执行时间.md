```js
/// 获得医嘱idStr，根据arcimId
/// 1-获取执行记录
/// 2-默认过滤未执行
/// 3-可以指定时间范围，过滤要求执行时间范围外的医嘱
/// ##class(Nur.Custom.Summary.Dll).GetOrderExecIdStrByArcimIdStr(4404084,"7908||1^7909||1","2020-01-01","00:00","2024-01-01","00:00")
ClassMethod GetOrderExecIdStrByArcimIdStr(EpisodeId, ArcimIdStr, StartDate, StartTime, EndDate, EndTime)
{
	d formatterDateTimeL
	s orderExecIdStr = ""
	s orderRid=$o(^OEORD(0,"Adm",EpisodeId,0)) 
	q:orderRid=""	
	s count=$l(ArcimIdStr,"^")
	f i=1:1:count{
		s arcimId = $p(ArcimIdStr,"^",i)
		continue:arcimId=""
		
		s orderStartDateL="" f {
			s orderStartDateL=$o(^OEORDi(0,"ARCIM",orderRid,arcimId,orderStartDateL))
			q:(orderStartDateL="")
			s orderSid="" f {
				s orderSid=$o(^OEORDi(0,"ARCIM",orderRid,arcimId,orderStartDateL,orderSid))
				q:orderSid=""
				//遍历执行记录
				s orderEid = 0
				f{
					s orderEid = $o(^OEORD(orderRid,"I",orderSid,"X",orderEid))
					q:orderEid=""
					//过滤要求执行时间
					s needToExucteDateL = $p(^OEORD(orderRid,"I",orderSid,"X",orderEid),"^",1)
					s needToExcuteTimeL = $p(^OEORD(orderRid,"I",orderSid,"X",orderEid),"^",2)
					s needToExcuteDateTimeL = needToExucteDateL*100000+needToExcuteTimeL
					continue:(needToExcuteDateTimeL<startDateTimeL)||(needToExcuteDateTimeL>endDateTimeL)
					//过滤未执行
					s excuteStatusCode = ""
				    s excuteStatusId = $p(^OEORD(orderRid,"I",orderSid,"X",orderEid),"^",16)
				    i $g(excuteStatusId)'="" s excuteStatusCode = $p(^OEC("STAT",xcuteStatusId),"^",1)
					continue:excuteStatusCode'["F"
					s execOrderId = orderRid_"||"_orderSid_"||"_orderEid
					s orderExecIdStr = $s(orderExecIdStr="":execOrderId,1:orderExecIdStr_"^"_execOrderId)
				}
			}
		}
	}
	q orderExecIdStr
formatterDateTimeL
	s StartDateL = ##Class(websys.Conversions).DateHtmlToLogical(StartDate)
	s StartTimeL = ##Class(websys.Conversions).TimeHtmlToLogical(StartTime)
	s startDateTimeL = StartDateL*100000+StartTimeL
	s EndDateL = ##Class(websys.Conversions).DateHtmlToLogical(EndDate)
	s EndTimeL = ##Class(websys.Conversions).TimeHtmlToLogical(EndTime)
	s endDateTimeL = EndDateL*100000+EndTimeL
}
```
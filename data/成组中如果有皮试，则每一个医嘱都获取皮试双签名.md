## 修改步骤

### 主方法
``` js
/// 成组医嘱如果有皮实，获取成组中的双签名
/// ##class(Nur.Custom.Order.UnitMethod).getPsOrderId(30907,22)
/// ##class(Nur.Custom.Order.UnitMethod).getPsOrderId(30907,23)
/// ##class(Nur.Custom.Order.UnitMethod).getPsOrderId(30907,24)
ClassMethod getPsOrderId(orderRid, orderSid)
{
	s result = ""
	s gourpOrderIdSr = ##class(Nur.Custom.Order.UnitMethod).getGroupOrderIdStr(orderRid, orderSid)
	q:gourpOrderIdSr="" result
	//判断是否有皮试医嘱
	s ifPs = ""
	f i=1:1:$l(gourpOrderIdSr,"^"){
		s orderId = $p(gourpOrderIdSr,"^",i)
		q:(orderId="")||(ifPs=1)
		s ifPs = ##class(Nur.Custom.Order.UnitMethod).ifPSOrder($p(orderId,"||",1),$p(orderId,"||",2))
	}
	q:ifPs'="1" result
	f i=1:1:$l(gourpOrderIdSr,"^"){
		s orderId = $p(gourpOrderIdSr,"^",i)
		q:(orderId="")||(result'="")
		s testRes=##Class(Nur.CommonInterface.OrderHandle).getSkinTestResult(orderId_"||"_1)
		s testResFirst=testRes.GetAt("testCtcpDesc")
		s testResSeconde=testRes.GetAt("auditCtcpDesc")
		i (testResFirst'="")&&(testResSeconde'="") s result=testResFirst_"/"_testResSeconde
	}
	q result
}
```

### 另外两个子方法

``` js
/// 判断是否有皮实医嘱
/// ##class(Nur.Custom.Order.UnitMethod).ifPSOrder(30907,22)
/// ##class(Nur.Custom.Order.UnitMethod).ifPSOrder(30907,23)
/// ##class(Nur.Custom.Order.UnitMethod).ifPSOrder(30907,24)
ClassMethod ifPSOrder(orderRid, orderSid)
{
	s result = ""
	s ifPsOrder = ##class(Nur.NIS.Service.Base.OrderHandle).ifSkinTestOrdNew(orderRid_"||"_orderSid) 
	i ifPsOrder="Y" s result = 1
	q result
}

/// 30907||22 30907||23 30907||24
/// ##class(Nur.Custom.Order.UnitMethod).getGroupOrderIdStr(30907,22)
/// ##class(Nur.Custom.Order.UnitMethod).getGroupOrderIdStr(30907,23)
/// ##class(Nur.Custom.Order.UnitMethod).getGroupOrderIdStr(30907,24)
ClassMethod getGroupOrderIdStr(orderRid, orderSid)
{
	s result = ""
	s mainOrderId = $p(^OEORD(orderRid,"I",orderSid,"11"),"^",39)
	
	i mainOrderId=""{
		s childSid = $o(^OEORDi(0,"OEORI",orderRid,orderRid_"||"_orderSid,""))
		i childSid'="" s mainOrderId=orderRid_"||"_orderSid
	}
	q:mainOrderId="" result
	s result=mainOrderId
	//获得子医嘱
	s childSid = ""
	f{
		s childSid = $o(^OEORDi(0,"OEORI",orderRid,mainOrderId,childSid))
		q:childSid=""
		s childOrderId = orderRid_"||"_childSid
		s result = result_"^"_childOrderId
		
	}
	q result
}
```